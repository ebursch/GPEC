      MODULE gslayer_mod
      
      USE sglobal_mod, ONLY: out_unit, r8, mu0, m_p, chag, lnLamb,
     $   Q_e,Q_i,pr,pe,c_beta,ds,tau,
     $   eta,visc,rho_s,lu,omega_e,omega_i,
     $   delta_n,
     $   Q
      USE delta_mod, ONLY: riccati,riccati_out,
     $   parflow_flag,PeOhmOnly_flag

      IMPLICIT NONE
      
      CONTAINS

c-----------------------------------------------------------------------
c     subprogram 1. gpec_slayer.
c     run slayer to provide b_crit(ising).
c-----------------------------------------------------------------------
c-----------------------------------------------------------------------
c     declarations.
c-----------------------------------------------------------------------
      SUBROUTINE gpec_slayer(n_e,t_e,n_i,t_i,zeff,omega,omega_e,
     $   omega_i,qval,sval,bt,rs,R0,mu_i,inpr,mms,nns,ascii_flag,
     $     delta,psi0,jxb,omega_sol,br_th)

      REAL(r8),INTENT(IN) :: n_e,t_e,n_i,t_i,omega,omega_e,omega_i,
     $     qval,sval,bt,rs,R0,zeff,inpr
      INTEGER, INTENT(IN) :: mms,nns,mu_i
      LOGICAL, INTENT(IN) :: ascii_flag
      COMPLEX(r8),INTENT(OUT) :: delta,psi0
      REAL(r8),INTENT(OUT) :: jxb,omega_sol,br_th
   
      INTEGER :: i,inum
      INTEGER, DIMENSION(1) :: index

      REAL(r8) :: inQ,inQ_e,inQ_i,inpe,inc_beta,inds,intau,inlu
      REAL(r8) :: mrs,nrs,rho,b_l,v_a,Qconv,Q0,delta_n_p,
     $            lbeta,tau_i,tau_h,tau_r,tau_v
      REAL(r8) :: inQ_min,inQ_max,Q_sol,maxbal
      REAL(r8) :: RR, PP, QQ
      INTEGER :: ipass
      INTEGER, PARAMETER :: nref=4
      REAL(r8) :: xpk,rlo,rhi,rdq,rdqc,xq,bloc,jloc
      COMPLEX(r8) :: dloc

      REAL(r8), DIMENSION(:), ALLOCATABLE :: inQs,iinQs,jxbl,bal
      COMPLEX(r8), DIMENSION(:), ALLOCATABLE :: deltal
      CHARACTER(3) :: sn,sm

      parflow_flag=.FALSE.
      PeOhmOnly_flag=.TRUE.
      riccati_out=.FALSE.

      mrs = real(mms,4)
      nrs = real(nns,4)

      ! String representations of the m and n mode numbers
      IF (nns<10) THEN
         WRITE(UNIT=sn,FMT='(I1)') nns
         sn=ADJUSTL(sn)
      ELSE
         WRITE(UNIT=sn,FMT='(I2)') nns
      ENDIF
      IF (mms<10) THEN
         WRITE(UNIT=sm,FMT='(I1)') mms
         sm=ADJUSTL(sm)
      ELSEIF (mms<100) THEN
         WRITE(UNIT=sm,FMT='(I2)') mms
         sm=ADJUSTL(sm)
      ELSE
         WRITE(UNIT=sm,FMT='(I3)') mms
      ENDIF

      inpe=0.0                         ! Waybright added this

      tau= t_i/t_e                     ! ratio of ion to electron temperature
      tau_i = 6.6e17*mu_i**0.5*(t_i/1e3)**1.5/(n_e*lnLamb) ! ion colls.
      eta= 1.65e-9*lnLamb/(t_e/1e3)**1.5 ! spitzer resistivity (wesson)
      rho=(mu_i*m_p)*n_e               ! mass density

      b_l=(nrs/mrs)*rs*sval*bt/R0      ! characteristic magnetic field
      v_a=b_l/(mu0*rho)**0.5           ! alfven velocity
      rho_s=1.02e-4*(mu_i*t_e)**0.5/bt ! ion Lamour by elec. Temp.

      tau_h=R0*(mu0*rho)**0.5/(nns*sval*bt) ! alfven time across surface
      tau_r=mu0*rs**2.0/eta            ! resistive time scale
      tau_v=tau_r/inpr                   ! rho*rs**2.0/visc ! viscous time scale

      ! this one must be anomalous. calculated back from pr.
      visc= rho*rs**2.0/tau_v

      lu=tau_r/tau_h                   ! Lundquist number

      Qconv=lu**(1.0/3.0)*tau_h        ! conversion to Qs based on Cole
      
      ! note Q depends on Qconv even if omega is fixed.     
      Q=Qconv*omega
      Q_e=-Qconv*omega_e
      Q_i=-Qconv*omega_i

      ! This is the most critical parameter
      ds=lu**(1.0/3.0)*rho_s/rs        ! conversion based on Cole.

      lbeta=(5.0/3.0)*mu0*n_e*chag*(t_e+t_i)/bt**2.0
      c_beta=(lbeta/(1.0+lbeta))**0.5

      delta_n=lu**(1.0/3.0)/rs         ! norm factor for delta primes

      inQ=Q
      inQ_e=Q_e
      inQ_i=Q_i
      inc_beta=c_beta
      inds=ds
      intau=tau
      Q0=Q
c-----------------------------------------------------------------------
c     calculate R, P, Q
c-----------------------------------------------------------------------
      RR = tau_h**(1.0_r8/15.0_r8)*
     $     (bt/b_l)**(2.0_r8/5.0_r8)/
     $     (tau_r*tau_v)**(1.0_r8/30.0_r8)

      PP = tau_r/tau_v

      QQ = tau_h**(2.0_r8/3.0_r8) * tau_r**(1.0_r8/3.0_r8) * omega
      WRITE(*,*)'!!!RR=',RR
      WRITE(*,*)'!!!PP=',PP
      WRITE(*,*)'!!!QQ=',QQ

      if (RR .lt. 1.0_r8) then
         write(*,'("Linear regime")')
      
      elseif (QQ .lt. PP**(-1.0_r8/3.0_r8)*RR**(-2.0_r8)) then
         write(*,'("Rutherford regime")')
      
      elseif (QQ .lt. PP**(-5.0_r8/42.0_r8)
     $        *(1.0_r8+PP)**(-3.0_r8/14.0_r8)
     $        *RR**(-5.0_r8/7.0_r8)) then
         write(*,'("Transition regime")')
      
      else
         write(*,'("Waelbroeck regime")')
      endif
c-----------------------------------------------------------------------
c     calculate basic delta, torque, balance, error fields.
c-----------------------------------------------------------------------
      delta_n_p=1e-2
      delta=riccati(inQ,inQ_e,inQ_i,inpr,inc_beta,inds,intau,inpe)
      psi0=1.0/ABS(delta+delta_n_p)     ! a.u.
      jxb=-AIMAG(1.0/(delta+delta_n_p)) ! a.u.
c-----------------------------------------------------------------------
c     find solutions based on simple torque balance.
c-----------------------------------------------------------------------
      IF (Q0>inQ_e) THEN
         inQ_max=2.0*Q0
         inQ_min=1.05*inQ_e
      ELSE
         inQ_max=0.95*inQ_e
         IF (Q0>0) THEN
            inQ_min=0.8*inQ_i
         ELSE
            inQ_min=1.5*MINVAL((/Q0,inQ_i/))
         ENDIF
      ENDIF

      ! Scan of rotation
      WRITE(*,*)'inQ_e=',inQ_e
      WRITE(*,*)'inQ_i=',inQ_i
      WRITE(*,*)'inpr=',inpr
      WRITE(*,*)'tau_r=',tau_r
      WRITE(*,*)'tau_h=',tau_h
      WRITE(*,*)'lu=',lu
       
      WRITE(*,*)'inc_beta=',inc_beta
      WRITE(*,*)'inds=',inds
      WRITE(*,*)'intau=',intau
      WRITE(*,*)'inpe=',inpe
      WRITE(*,*)'omega=',omega
      WRITE(*,*) 'n_e=',n_e
      WRITE(*,*) 't_e=',t_e
      WRITE(*,*) 'n_i=',n_i
      WRITE(*,*) 't_i=',t_i
      WRITE(*,*) 'zeff=',zeff
      WRITE(*,*) 'omega_e=',omega_e
      WRITE(*,*) 'omega_i=',omega_i
      WRITE(*,*) 'qval=',qval
      WRITE(*,*) 'sval=',sval
      WRITE(*,*) 'bt=',bt
      WRITE(*,*) 'rs=',rs
      WRITE(*,*) 'R0=',R0
      WRITE(*,*) 'mu_i=',mu_i
      WRITE(*,*) 'inpr=',inpr
      WRITE(*,*) 'delta=',delta
      WRITE(*,*) 'psi0=',psi0
      WRITE(*,*) 'jxb=',jxb
      WRITE(*,*) 'omega_sol=',omega_sol
      WRITE(*,*) 'br_th=',br_th      

      inQ_max=10.0
      inQ_min=-10.0
      inum=200
      ALLOCATE(inQs(0:inum),deltal(0:inum),jxbl(0:inum),bal(0:inum)) 
      DO i=0,inum
         inQs(i)=inQ_min+(REAL(i)/inum)*(inQ_max-inQ_min)
         deltal(i)=riccati(inQs(i),inQ_e,inQ_i,
     $        inpr,inc_beta,inds,intau,inpe)
         jxbl(i)=-AIMAG(1.0/(deltal(i)+delta_n_p))
         bal(i)=2.0*inpr*(Q0-inQs(i))/jxbl(i)
      ENDDO

      ! Write torque balance curves to file for diagnostic purposes
      IF(ascii_flag)THEN
         OPEN(UNIT=out_unit,FILE="gpec_slayer_torque_balance_m"//
     $        TRIM(sm)//"_n"//TRIM(sn)//".out",
     $        STATUS="UNKNOWN")
         WRITE(out_unit,'(1x,5(a17))') "inQ","RE(delta)",
     $        "IM(delta)","jxb","bal"
         DO i=0,inum
            WRITE(out_unit,'(1x,5(es17.8e3))')
     $           inQs(i),REAL(deltal(i)),AIMAG(deltal(i)),jxbl(i),bal(i)
         ENDDO
         CLOSE(out_unit)
      ENDIF

      ! The coarse scan above gives the overall torque-balance shape
      ! and the diagnostic output. The locking "nose" (the maximum of
      ! bal) can be a very narrow spike near inQ=Q0, however, and the
      ! coarse grid can step right over it, leaving a spuriously
      ! negative MAXVAL(bal) and hence a NaN br_th. Bracket the coarse
      ! maximum and iteratively refine the scan around the running peak
      ! so the nose is resolved regardless of how narrow it is. Exclude
      ! non-finite entries (NaN from 0/0, Inf from a near-zero jxbl).
      index=MAXLOC(bal,MASK=(bal==bal .AND. ABS(bal)<HUGE(bal)))
      rdqc=(inQ_max-inQ_min)/inum
      xpk=inQs(index(1))
      rlo=xpk-2.0*rdqc
      rhi=xpk+2.0*rdqc
      maxbal=-HUGE(maxbal)
      DO ipass=1,nref
         rdq=(rhi-rlo)/inum
         DO i=0,inum
            xq=rlo+REAL(i)*rdq
            dloc=riccati(xq,inQ_e,inQ_i,inpr,
     $           inc_beta,inds,intau,inpe)
            jloc=-AIMAG(1.0/(dloc+delta_n_p))
            bloc=2.0*inpr*(Q0-xq)/jloc
            IF (bloc==bloc .AND. ABS(bloc)<HUGE(bloc)
     $           .AND. bloc>maxbal) THEN
               maxbal=bloc
               xpk=xq
            ENDIF
         ENDDO
         rlo=xpk-2.0*rdq
         rhi=xpk+2.0*rdq
      ENDDO
      Q_sol=xpk
      omega_sol=xpk/Qconv
      ! If even the refined nose is non-positive the surface has no
      ! finite penetration threshold; floor at zero and warn rather
      ! than propagating a NaN into b_crit/Phi_res_crit downstream.
      IF (maxbal>0.0_r8) THEN
         br_th=sqrt(maxbal/lu*(sval**2.0/2.0))
      ELSE
         br_th=0.0_r8
         WRITE(*,'(1x,a,i0,a,i0,a)')
     $      "!! WARNING: SLAYER torque balance has no positive "//
     $      "maximum at m=",mms,", n=",nns,"; br_th set to 0"
      ENDIF
      WRITE(*,*)'inQs=',inQs
      WRITE(*,*)'deltal=',deltal
      WRITE(*,*)'jxbl=',jxbl
      WRITE(*,*)'bal=',bal
      WRITE(*,*)'index=',index
      WRITE(*,*)'Q_sol=',Q_sol
      WRITE(*,*)'omega_sol=',omega_sol
      WRITE(*,*)'br_th=',br_th
      DEALLOCATE(inQs,deltal,jxbl,bal)

      RETURN
      END SUBROUTINE gpec_slayer

      END MODULE gslayer_mod


